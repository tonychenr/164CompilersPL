/*
 * cool.cup
 *
 * CS164: Spring 2004 
 * Programming Assignment 3
 *
 * Parser definition for the Cool language.
 *
 */

import java_cup.runtime.*;

/* Code enclosed in {: :} is copied verbatim to the class containing
all parser actions.  All the extra variables/functions you want to use
in the semantic actions should go here.  Don't remove or modify anything
that was there initially.  */

action code {: 

    int curr_lineno() {
	return ((CoolTokenLexer)parser.getScanner()).curr_lineno();
    }

    AbstractSymbol curr_filename() {
	return ((CoolTokenLexer)parser.getScanner()).curr_filename();
    }

:} 

/************************************************************************/
/*                DON'T CHANGE ANYTHING IN THIS SECTION                 */

parser code {:
    int omerrs = 0;

    public void syntax_error(Symbol cur_token) {
        int lineno = action_obj.curr_lineno();
	String filename = action_obj.curr_filename().getString();
        System.err.print("\"" + filename + "\", line " + lineno + 
		         ": parse error at or near ");
        Utilities.printToken(cur_token);
	omerrs++;
	if (omerrs>50) {
	   System.err.println("More than 50 errors");
	   System.exit(1);
	}
    }

    public void unrecovered_syntax_error(Symbol cur_token) {
    }
:}


/* Declare the terminals; a few have types for associated lexemes.  The
token ERROR is never used in the parser; thus, it is a parse error when
the lexer returns it.  */
terminal CLASS, ELSE, FI, IF, IN, INHERITS, LET, LET_STMT, LOOP, POOL, THEN, WHILE;
terminal CASE, ESAC, OF, DARROW, NEW, ISVOID;
terminal ASSIGN, NOT, LE, ERROR;
terminal PLUS, DIV, MINUS, MULT, EQ, LT, DOT, NEG, COMMA, SEMI, COLON;
terminal LPAREN, RPAREN, AT, LBRACE, RBRACE;
terminal AbstractSymbol STR_CONST, INT_CONST;
terminal Boolean BOOL_CONST;
terminal AbstractSymbol TYPEID, OBJECTID;

/*  DON'T CHANGE ANYTHING ABOVE THIS LINE, OR YOUR PARSER WON'T WORK      */
/**************************************************************************/


/*
 * Declare types for the grammar's non-terminals.
 *
 * Hint: You will have to add/modify this list.
 */
nonterminal programc program;
nonterminal Classes class_list;
nonterminal class_c class_cc;
nonterminal Features optional_feature_list;
nonterminal Feature feature;
nonterminal Formals formal_list;
nonterminal formalc formal;
nonterminal Expression expr;
nonterminal Expressions actuals, exp_list;
nonterminal Cases case_list;
nonterminal Case case_c;
nonterminal Expressions block_expr;
nonterminal Expression multi_let;


/*
 * Precedence declarations.
 */
/*** FILL-IN HERE ***/

precedence left ASSIGN;
precedence left NOT;
precedence left LE, LT, EQ;
precedence left PLUS, MINUS;
precedence left MULT, DIV;
precedence left ISVOID;
precedence left NEG;
precedence left AT;
precedence left DOT;







/***********************************************************************
 * Grammar rules.                                                      *
 *                                                                     *
 * Some parts of rules have been provided for you as an example and    *
 * to get you started.  You should not need to modify this code to get *
 * a working solution, but you are welcome to if you like.  However,   *
 * do not assume that any particular rule is complete.                 *
 ***********************************************************************/

/* Program */
program	
	::= class_list:cl
	    {: RESULT = new programc(curr_lineno(), cl); :}
	| error
	    {: RESULT = new programc(curr_lineno(),
                                     new Classes(curr_lineno())); :}
        ;


class_list
	/* single class */
	::= class_cc:c
	    {: RESULT = (new Classes(curr_lineno())).appendElement(c); :}
	/* several classes */
	| class_list:cl class_cc:c
	    {: RESULT = cl.appendElement(c); :}
	| error
	;


/* Class declarations
 *
 * If no parent is specified, the class inherits from the Object class.
 */
class_cc
	::= CLASS TYPEID:n LBRACE optional_feature_list:f RBRACE SEMI
	    {: RESULT = new class_c(curr_lineno(), n, 
		                    AbstractTable.idtable.addString("Object"), 
				    f, curr_filename()); :}
	| CLASS TYPEID:n INHERITS TYPEID:p LBRACE optional_feature_list:f RBRACE SEMI
	    {: RESULT = new class_c(curr_lineno(), n, p, f, curr_filename()); :}
	| error
	;


optional_feature_list
	::= /* empty */
	    {: RESULT = new Features(curr_lineno()); :}
	/* nonempty feature list */
	| optional_feature_list:fl feature:f
		{: RESULT = fl.appendElement(f); :}
	| error
	;


feature
	/* attribute declaration without assignment */
	::= OBJECTID:obj COLON TYPEID:type SEMI
		{: RESULT = new attr(curr_lineno(), obj, type, new no_expr(curr_lineno())); :}
	/* attribute declaration with assignment */
	| OBJECTID:obj COLON TYPEID:type ASSIGN expr:e SEMI
		{: RESULT = new attr(curr_lineno(), obj, type, e); :}
	/* method declaration with no formals */
	| OBJECTID:obj LPAREN RPAREN COLON TYPEID:type LBRACE expr:e RBRACE SEMI
		{: RESULT = new method(curr_lineno(), obj, new Formals(curr_lineno()), type, e); :}
	/* method declaration with nonempty formal list */
	| OBJECTID:obj LPAREN formal_list:fl RPAREN COLON TYPEID:type LBRACE expr:e RBRACE SEMI
		{: RESULT = new method(curr_lineno(), obj, fl, type, e); :}
	| error
	;


formal_list
	/* single formal */
	::= formal:f
		{: RESULT = new Formals(curr_lineno()).appendElement(f); :}
	/* multiple formals */
	| formal_list:fl COMMA formal:f
		{: RESULT = fl.appendElement(f); :}
	| error
	;

/* an element of formal_list */
formal
	::= OBJECTID:obj COLON TYPEID:type
		{: RESULT = new formalc(curr_lineno(), obj, type); :}
	| error
	;


/* Expressions */
expr
	::= expr:e1 PLUS expr:e2
	    {: RESULT = new plus(curr_lineno(), e1, e2); :}
	| expr:e1 MINUS expr:e2
	    {: RESULT = new sub(curr_lineno(), e1, e2); :}
	| expr:e1 MULT expr:e2
	    {: RESULT = new mul(curr_lineno(), e1, e2); :}
	| expr:e1 DIV expr:e2
	    {: RESULT = new divide(curr_lineno(), e1, e2); :}
	| NEG expr:e
	    {: RESULT = new neg(curr_lineno(), e); :}
	| expr:e1 LT expr:e2
	    {: RESULT = new lt(curr_lineno(), e1, e2); :}
	| expr:e1 EQ expr:e2
	    {: RESULT = new eq(curr_lineno(), e1, e2); :}
	| expr:e1 LE expr:e2
	    {: RESULT = new leq(curr_lineno(), e1, e2); :}
	| NOT expr:e
	    {: RESULT = new comp(curr_lineno(), e); :}
	| LPAREN expr:e RPAREN
	    {: RESULT = e; :}
	| INT_CONST:i
	    {: RESULT = new int_const(curr_lineno(), i); :}
	| STR_CONST:s
	    {: RESULT = new string_const(curr_lineno(), s); :}
	| BOOL_CONST:b
	    {: RESULT = new bool_const(curr_lineno(), b); :}
	| OBJECTID:o
	    {: RESULT = new object(curr_lineno(), o); :}
	| OBJECTID:n actuals:a
	    {: RESULT = new dispatch(curr_lineno(),
	                             new object(curr_lineno(), 
	                                        AbstractTable.idtable.addString("self")),
				     n, a); :}
       
	/* assignment expression */
	| OBJECTID:obj ASSIGN expr:e
		{: RESULT = new assign(curr_lineno(), obj, e); :}
	/* new object */
	| NEW TYPEID:type
		{: RESULT = new new_(curr_lineno(), type); :}
	/* isvoid expression */
	| ISVOID expr:e
		{: RESULT = new isvoid(curr_lineno(), e); :}
	/* case expression */
	| CASE expr:e OF case_list:cl ESAC
		{: RESULT = new typcase(curr_lineno(), e, cl); :}
	/* block expression */
	| LBRACE block_expr:eb RBRACE
		{: RESULT = new block(curr_lineno(), eb); :}
	/* if then else */
	| IF expr:e1 THEN expr:e2 ELSE expr:e3 FI
		{: RESULT = new cond(curr_lineno(), e1, e2, e3); :}
	/* loop */
	| WHILE expr:e1 LOOP expr:e2 POOL
		{: RESULT = new loop(curr_lineno(), e1, e2); :}
	/* dispatch*/
	| expr:e1 DOT OBJECTID:obj actuals:a
		{: RESULT = new dispatch(curr_lineno(), e1, obj, a); :}
	/* static dispatch */
	| expr:e1 AT TYPEID:type DOT OBJECTID:obj actuals:a
		{: RESULT = new static_dispatch(curr_lineno(), e1, type, obj, a); :}
	/* let expression without initialization */
	| LET OBJECTID:obj COLON TYPEID:type IN expr:e
		{: RESULT = new let(curr_lineno(), obj, type, new no_expr(curr_lineno()), e); :}
	/* let expression with initialization*/
	| LET OBJECTID:obj COLON TYPEID:type ASSIGN expr:asgn IN expr:e
		{: RESULT = new let(curr_lineno(), obj, type, asgn, e); :}
	/* let expression with initilization, multiple variables */
	| LET OBJECTID:obj COLON TYPEID:type ASSIGN expr:asgn COMMA multi_let:e
		{: RESULT = new let(curr_lineno(), obj, type, asgn, e); :}
	/* let expression with without initialization, multiple variables */
	| LET OBJECTID:obj COLON TYPEID:type COMMA multi_let:e
		{: RESULT = new let(curr_lineno(), obj, type, new no_expr(curr_lineno()), e); :}
	| error
    ;

/* Multiple let expression variables chained to allow multiple initializations */
multi_let
	/* let expression variable with initialization, multiple variables */
	::= OBJECTID:obj COLON TYPEID:type ASSIGN expr:asgn COMMA multi_let:e
		{: RESULT = new let(curr_lineno(), obj, type, asgn, e); :}
		/* let expression variable without initialization, multiple variables */
	| OBJECTID:obj COLON TYPEID:type COMMA multi_let:e
		{: RESULT = new let(curr_lineno(), obj, type, new no_expr(curr_lineno()), e); :}
		/* end of let expression with initialized variable */
	| OBJECTID:obj COLON TYPEID:type ASSIGN expr:asgn IN expr:e
		{: RESULT = new let(curr_lineno(), obj, type, asgn, e); :}
		/* end of let expression without initialized variable */
	| OBJECTID:obj COLON TYPEID:type IN expr:e
		{: RESULT = new let(curr_lineno(), obj, type, new no_expr(curr_lineno()), e); :}
	| error
		//{: RESULT = new let(curr_lineno(), obj, type, new no_expr(curr_lineno()), e); :}
	;

/* list of cases for case expression */
case_list
	/* single case */
	::= case_c:c
		{: RESULT = new Cases(curr_lineno()).appendElement(c); :}
	/* multiple cases */
	| case_list:cl case_c:c
		{: RESULT = cl.appendElement(c); :}
	| error
	;

/* an element of case_list */
case_c
	::= OBJECTID:obj COLON TYPEID:type DARROW expr:e SEMI
		{: RESULT = new branch(curr_lineno(), obj, type, e); :}
	| error
	;


block_expr
	/* single expression */
	::= expr:e SEMI
		{: RESULT = new Expressions(curr_lineno()).appendElement(e); :}
	/* several expressions */
	| block_expr:eb expr:e SEMI
		{: RESULT = eb.appendElement(e); :}
	;


/* Method arguments */
actuals
        /* No arguments */
	::= LPAREN RPAREN
	    {: RESULT = new Expressions(curr_lineno()); :}
        /* List of arguments */
	| LPAREN exp_list:el RPAREN
	    {: RESULT = el; :}
	| error
	;


/* Non-empty list of expressions */
exp_list
        /* One expression */
	::= expr:e
	    {: RESULT = (new Expressions(curr_lineno())).appendElement(e); :} 
        /* Several expressions */
	| exp_list:el COMMA expr:e
	    {: RESULT = el.appendElement(e); :}
	| error
	;
